// Bintray section

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

ext {
    bintrayRepo = 'maven'
    bintrayName = 'keasy'

    publishedGroupId = 'com.github.hivian'
    libraryName = 'keasy'
    artifact = 'keasy'

    libraryDescription = 'Kotlin extensions and utility functions for Android'
    libraryTags = ['android', 'kotlin', 'extension', 'methods', 'utils']

    siteUrl = 'https://github.com/hivian/' + bintrayName
    gitUrl = siteUrl + '.git'
    gitTrackerUrl = siteUrl + '/issues'

    libraryVersion = appVersion.name

    developerId = 'hivian'
    developerName = 'hivian'
    developerEmail = 'hivian@student.42.fr'

    licenseName = 'The MIT License (MIT)'
    licenseUrl = 'https://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}

// Maven section

group = publishedGroupId // Maven Group ID for the artifact

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId artifact

                // Add your description here
                name libraryName
                description libraryDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

// Bintray section

version = libraryVersion

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }
}

artifacts {
    archives sourcesJar
}

// If local publish
//Properties properties = new Properties()
//properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    // If local publish
    //user properties.getProperty('bintray.user')
    //key properties.getProperty('bintray.apikey')
    // Travis environment variable
    user System.getenv('BINTRAY_USER')
    key System.getenv('BINTRAY_API_KEY')

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = gitTrackerUrl
        labels = libraryTags
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                // optional GPG encryption. Default is false.
                sign = false
                //passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}
